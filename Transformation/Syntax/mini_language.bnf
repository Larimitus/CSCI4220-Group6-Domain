USE_LR_PARSER

prog

(* ======================================================================================================= *)
(*                                             basic expressions                                           *)
(* ======================================================================================================= *)
<prog>               ::= <StmtList>                                                                     .

<StmtList>           ::= <Stmt> ";" <StmtList>
                      | <Stmt> ";"                                                                      .

<Stmt>               ::= <Declare>
                      | <Assign>
                      | <Print>
                      | <Cond>
                      | <Iter>
                      | <Block>                                                                         .

<Declare>            ::= "int" identifier
                      | "bool" identifier                                                               .

<Assign>             ::= <IncrDecr>
                      | "bool" identifier "=" <Express>
                      | "int" identifier "=" <Express>
                      | identifier "=" <Express>                                                        .

<Block>              ::= "{" <StmtList> "}"                                                             .

<Cond>               ::= <If>
                      | <IfElse>                                                                        .

<If>                 ::= "if" "(" <Express> ")" "then" <Block>                                          .

<IfElse>             ::= "if" "(" <Express> ")" "then" <Block> "else" <Block>                           .

<IncrDecr>            ::= "++" identifier
                      | "--" identifier
                      | identifier "++"
                      | identifier "--"                                                                 .

<Iter>               ::= <ForIter>
                      | <WhileIter>
                      | <RepeatUntil>                                                                   .

<ForIter>            ::= "for" "(" <Assign> "," <Express> "," <Assign> ")" <Block>                      .

<WhileIter>          ::= "while" "(" <Express> ")" <Block>                                              .

<RepeatUntil>        ::= "repeat" <Block> "until" "(" <Express> ")"                                     .

<Express>            ::= <LogicOr>                                                                      .

<LogicAnd>           ::= <LogicAnd> "&&" <LogicEqual>
                      | <LogicEqual>                                                                    .

<LogicEqual>         ::= <LogicEqual> "==" <RelOp>
                      | <LogicEqual> "!=" <RelOp>
                      | <RelOp>                                                                         .

<LogicOr>            ::= <LogicOr> "||" <LogicAnd>
                      | <LogicAnd>                                                                      .

<RelOp>              ::= <AddOp> "<" <AddOp>
                      | <AddOp> "<=" <AddOp>
                      | <AddOp> ">" <AddOp>
                      | <AddOp> ">=" <AddOp>
                      | <AddOp>                                                                         .

<AddOp>              ::= <AddOp> "+" <MultiOp>
                      | <AddOp> "-" <MultiOp>
                      | <MultiOp>                                                                       .

<MultiOp>            ::= <MultiOp> "*" <UniMin>
                      | <MultiOp> "/" <UniMin>
                      | <MultiOp> "%" <UniMin>
                      | <UniMin>                                                                        .

<UniMin>             ::= "-" <UniMin>
                      | <ExpOp>                                                                         .

<ExpOp>              ::= <Ops> "^" <ExpOp>
                      | <Ops>                                                                           .

<Ops>                ::= identifier
                      | "(" <Express> ")"
                      | "|" <Express> "|"
                      | <IncrDecr>
                      | "not" "(" <Express> ")"
                      | <Value>                                                                         .

<Value>              ::= integer
                      | "true"
                      | "false"                                                                         .

<Print>              ::= "print" "(" <Express> ")"                                                      .
(* ======================================================================================================= *)